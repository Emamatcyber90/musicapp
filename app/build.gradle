apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../config/android_application_quality.gradle'

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.sniper.music"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode rootProject.ext.versionCode as Integer
        versionName rootProject.ext.versionName

        testInstrumentationRunner getInstrumentation()

        resConfigs "en"
    }
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug

            buildConfigField 'String', 'TEST_TAGS', '"' + getTestTags() + '"'
            buildConfigField 'String', 'TEST_SCENARIO', '"' + getTestScenario() + '"'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def getInstrumentation() {
    project.hasProperty('cucumber') ? 'com.sniper.music.cucumber.runner.CucumberTestRunner' : 'com.sniper.music.espresso.runner.MockTestRunner'
}

def getTestTags() {
    project.getProperties().get('tags') ?: ''
}

def getTestScenario() {
    project.getProperties().get('scenario') ?: ''
}

dependencies {
    implementation project(':home')
    implementation project(':details')

    androidTestAnnotationProcessor rootProject.libraries.daggerCompiler
    androidTestAnnotationProcessor rootProject.libraries.javaxAnnotationProcessor

    commonAndroidTestDependencies(it)
    mockServerTestDependencies(it)

}
